/*
 * generated by Xtext 2.21.0
 */
package org.xtext.example.mydsl.generator

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext
import org.xtext.example.mydsl.stateMachine.StateMachine
import org.xtext.example.mydsl.stateMachine.Instruction
import org.eclipse.xtext.naming.IQualifiedNameProvider
import javax.inject.Inject
import org.xtext.example.mydsl.stateMachine.State

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class StateMachineGenerator extends AbstractGenerator {
	
	
	override void doGenerate(Resource res, IFileSystemAccess2 fsa, IGeneratorContext context) {
		for (e : res.allContents.toIterable.filter(StateMachine)){
			fsa.generateFile(e.eResource.name+".py", e.compileStateMachine)
			}
	}
	
	def CharSequence compileStateMachine(StateMachine model)
		'''
			class «model.eResource.name»():
			
			
				«FOR c : model.instructions»
					«c.generateInstructions»
				«ENDFOR»
			
				def run(self):
					self.currentState = "«model.state.head?.name»"
					self.previousEvent = None
					self.executeActions = True
					
					while(True):
						«FOR state : model.state»
							«state.generateActions»
						«ENDFOR»
						«FOR eventReset : model.eventReset»
							if(self.previousEvent == '«eventReset.name»'):
								print('Resetting')
								currentState = '«model.state.head?.name»'
								executeActions = True
						«ENDFOR»
					
					«FOR c : model.instructions»
						self.«c.name.toFirstLower»()
					«ENDFOR»
					
					
				def eventHandler(self):
					«generateInputHandler»
						
				
			if __name__ == '__main__':
				state = «model.eResource.name»()
				state.run()

		'''
	
	def generateInputHandler() {
		return
		'''
		n = input('Enter state: ')
		n = str(n)
		return n
		'''
	}
		
	def generateActions(State state) 
	'''
		if(self.currentState == '«state.name»'):
			if(self.executeActions):
				«FOR e : state.actions»
					self.«e.name.toFirstLower»()
				«ENDFOR»
				self.executeActions = False
				
			print('Current state is:  «state.name».')
			self.previousEvent = self.eventHandler()
			«FOR c : state.moves»
				if('«c.event.name»' == self.previousEvent):
					self.currentState = '«c.state.name»'
					self.executeActions = True
			«ENDFOR»

	'''
	
	
	def generateInstructions(Instruction ins)
	'''
		def «ins.name.toFirstLower»(self):
			print('Running instruction «ins.name»')
	'''
	


	
	def getName(Resource resource){
		var name = resource.URI.lastSegment
		return name.substring(0, name.indexOf('.')).toFirstUpper
	}
	
	
}